buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

version = "2.0.0"

group = "com.simon816.chatui"

repositories {
    mavenCentral()
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }
    // For launcherwrapper needed by mixin
    maven {
        name = 'mojang'
        url = 'https://libraries.minecraft.net/'
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    // Stop AP generating a mcmod.info file
    options.compilerArgs += '-proc:none'
}

configurations {
    shaded
    compile {
        extendsFrom shaded
    }
}

dependencies {
    compile 'org.spongepowered:spongeapi:7.0.0'

    compile project(':ChatUILib')

    compile ('org.spongepowered:spongecommon:7.0.0-SNAPSHOT:dev') {
        exclude group: 'org.spongepowered', module: 'testplugins'
    }
}

processResources {
    from('src/main/resources') {
        include 'mcmod.info'
        // Version set in gradle.properties
        expand(version: project.version, apiVersionRange: apiVersionRange, libVersionRange: libVersionRange)
    }
}

jar {
    classifier = 'dev'
}

task srcJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

shadowJar {
    configurations = [project.configurations.shaded]
    classifier = ''
    baseName = "ChatUI"
}

assemble.dependsOn = [jar, srcJar, shadowJar]

artifacts {
    archives shadowJar
}

